{-# LANGUAGE OverloadedStrings #-}
{-# LANGUAGE QuasiQuotes #-}
{-# LANGUAGE RecordWildCards #-}
{-# LANGUAGE TypeApplications #-}
{-# LANGUAGE ViewPatterns #-}

module Development.NvFetcher
  ( module Development.NvFetcher.NixFetcher,
    module Development.NvFetcher.Nvchecker,
    module Development.NvFetcher.PackageSet,
    module Development.NvFetcher.Types,
    nvfetcherRules,
    generateNixSources,
    Args (..),
    defaultArgs,
    defaultMain,
    defaultMainWith,
    VersionChange (..),
    getVersionChanges,
  )
where

import Control.Concurrent.MVar (MVar, modifyMVar_, newMVar, readMVar)
import Data.Coerce (coerce)
import Data.Maybe (fromJust, fromMaybe)
import qualified Data.Set as Set
import qualified Data.Text as T
import Development.NvFetcher.NixFetcher
import Development.NvFetcher.Nvchecker
import Development.NvFetcher.PackageSet
import Development.NvFetcher.Types
import Development.Shake
import NeatInterpolation (trimming)
import System.Console.GetOpt (OptDescr)

data Args = Args
  { argShakeOptions :: ShakeOptions -> ShakeOptions,
    argOutputFilePath :: FilePath,
    argRules :: Rules (),
    argActionAfterBuild :: Action ()
  }

defaultArgs :: Args
defaultArgs =
  Args
    ( \x ->
        x
          { shakeTimings = True,
            shakeProgress = progressSimple
          }
    )
    "sources.nix"
    (pure ())
    (pure ())

defaultMain :: Args -> PackageSet () -> IO ()
defaultMain args packageSet = defaultMainWith [] $ const $pure (args, packageSet)

defaultMainWith :: [OptDescr (Either String a)] -> ([a] -> IO (Args, PackageSet ())) -> IO ()
defaultMainWith flags f = do
  var <- newMVar mempty
  shakeArgsOptionsWith
    shakeOptions
    flags
    $ \opts flagValues argValues -> case argValues of
      [] -> pure Nothing
      files -> do
        (args@Args {..}, packageSet) <- f flagValues
        let opts' = argShakeOptions opts
        pure $
          Just
            ( opts'
                { shakeExtra = addShakeExtra (VersionChanges var) (shakeExtra opts')
                },
              want files >> mainRules args packageSet
            )

mainRules :: Args -> PackageSet () -> Rules ()
mainRules Args {..} packageSet = do
  "clean" ~> do
    removeFilesAfter ".shake" ["//*"]
    removeFilesAfter "." [argOutputFilePath]

  "build" ~> do
    pkgs <- runPackageSet packageSet
    generateNixSources argOutputFilePath $ Set.toList pkgs
    argActionAfterBuild

  argRules
  nvfetcherRules

--------------------------------------------------------------------------------

data VersionChange = VersionChange
  { vcName :: PackageName,
    vcOld :: Maybe Version,
    vcNew :: Version
  }
  deriving (Eq)

instance Show VersionChange where
  show VersionChange {..} =
    T.unpack $ vcName <> ": " <> fromMaybe "∅" (coerce vcOld) <> " → " <> coerce vcNew

newtype VersionChanges = VersionChanges (MVar [VersionChange])

recordVersionChange :: PackageName -> Maybe Version -> Version -> Action ()
recordVersionChange vcName vcOld vcNew = do
  VersionChanges var <- fromJust <$> getShakeExtra @VersionChanges
  liftIO $ modifyMVar_ var (pure . (++ [VersionChange {..}]))

getVersionChanges :: Action [VersionChange]
getVersionChanges = do
  VersionChanges var <- fromJust <$> getShakeExtra @VersionChanges
  liftIO $ readMVar var

--------------------------------------------------------------------------------

nvfetcherRules :: Rules ()
nvfetcherRules = do
  nvcheckerRule
  prefetchRule

generateNixSources :: FilePath -> [Package] -> Action ()
generateNixSources fp pkgs = do
  body <- fmap genOne <$> actions
  getVersionChanges >>= \changes ->
    if null changes
      then putInfo "Up to date"
      else do
        putInfo "Changes:"
        putInfo $ unlines $ show <$> changes
  writeFileChanged fp $ T.unpack $ srouces $ T.unlines body
  putInfo $ "Generate " <> fp
  where
    single Package {..} = do
      (NvcheckerResult version mOld) <- checkVersion pversion
      prefetched <- prefetch $ pfetcher version
      case mOld of
        Nothing ->
          recordVersionChange pname Nothing version
        Just old
          | old /= version ->
            recordVersionChange pname (Just old) version
        _ -> pure ()
      pure (pname, version, prefetched)
    genOne (name, coerce @Version -> ver, toNixExpr -> srcP) =
      [trimming|
        $name = {
          pname = "$name";
          version = "$ver";
          src = $srcP;
        };
      |]
    actions = parallel $ map single pkgs
    srouces body =
      [trimming|
        # This file was generated by nvfetcher, please do not modify it manually.
        { fetchgit, fetchurl }:
        {
          $body
        }
      |]
